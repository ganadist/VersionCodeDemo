def hasDynamicFeatureModulePlugin = pluginManager.hasPlugin("com.android.dynamic-feature")
def hasKotlinPlugin = pluginManager.hasPlugin("kotlin-android")                                      

android {
    compileSdkVersion "android-30"
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion "30"
	    compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8                                              
            targetCompatibility JavaVersion.VERSION_1_8 
    	}

        if (hasKotlinPlugin) {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8.toString()                                       
                allWarningsAsErrors = true
            }
        }

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments.putIfAbsent("clearPackageData", "true")
    }

    testOptions {
        execution = "ANDROIDX_TEST_ORCHESTRATOR"

        unitTests {
            // https://github.com/robolectric/robolectric/issues/5597#issuecomment-876564570
            includeAndroidResources = !hasDynamicFeatureModulePlugin

            all {
                testLogging {
                    events "standardOut", "started", "passed", "failed", "skipped"
                    showCauses = true
                    showExceptions = true
                    showStackTraces = true
                    showStandardStreams = true

                }
                systemProperty "robolectric.logging", "stdout"
            }
        }
    }
}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

configurations.all {
    // Conflicted with Android platform API such as org.apache.http
    exclude group: "org.apache.httpcomponents"
    // Conflicted with Android platform API such as org.json
    exclude group: "org.json", module: "json"
    // Conflicted with Android platform API such as javax.xml, org.xml, and org.w3c.dom
    exclude group: "xalan", module: "xalan"
    // Conflicted with Android platform API such as org.xmlpull
    exclude group: "org.ogce", module: "xpp3"
    exclude group: "xpp3", module: "xpp3"
}

dependencies {
    testImplementation libs.junit4
    testImplementation libs.robolectric.core
    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin

    testImplementation androidx.test.ktx
    testImplementation androidx.test.rules
    testImplementation androidx.test.runner
    testImplementation androidx.test.ext.junit.ktx

    androidTestImplementation androidx.test.ext.junit.ktx
    androidTestImplementation androidx.test.espresso.core
}
