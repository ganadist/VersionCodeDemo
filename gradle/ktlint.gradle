// https://github.com/pinterest/ktlint#without-a-plugin

configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest:ktlint:0.47.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    args(
        "--android",
        "--debug",
        "lib/hostconfig/src/main/**.kt",
        "!**/build/generated/**", "!**/build/kspCaches/**"
    )
}

task ktlintFormat(type: JavaExec, group: "verification") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    jvmArgs = ["--add-opens=java.base/java.lang=ALL-UNNAMED"]
    args(
        "--android", "-F",
        "lib/hostconfig/src/main/**.kt",
        "!**/build/generated/**", "!**/build/kspCaches/**"
    )
}

