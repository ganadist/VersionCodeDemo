pluginManagement {

    // https://issuetracker.google.com/issues/258270051#comment16
    buildscript {
        repositories {
            mavenCentral()
            maven {
                url = uri("https://storage.googleapis.com/r8-releases/raw")
            }
        }

        dependencies {
            if (!r8Version.isEmpty()) {
                logger.warn("R8 $r8Version will be applied")
                classpath "com.android.tools:r8:$r8Version"
	    }
	}
    }

    apply from: "$settingsDir/gradle/repositories.gradle"
    settings.ext.repos.google(repositories)
    settings.ext.repos.gradlePluginPortal(repositories)

    plugins {
        id 'com.android.settings' version "$androidGradlePluginVersion"
    }

    includeBuild("build-logic")
}

plugins {
    id 'com.android.settings'
}

android {
    def _minSdk = settings.getProperty("minSdk").toInteger()
    def _compileSdkPreview = settings.getProperty("compileSdkPreview")
    def _buildToolsVersion = settings.getProperty("buildToolsVersion")

    compileSdk settings.getProperty("compileSdk").toInteger()
    compileSdkExtension settings.getProperty("compileSdkExtension").toInteger()

    if (!_compileSdkPreview.isEmpty()) {
        compileSdkPreview _compileSdkPreview
    }

    minSdk _minSdk
    ndkVersion "$NDK_VERSION"
    buildToolsVersion _buildToolsVersion
    execution {
        defaultProfile "minimal"
        profiles {
            minimal {
                r8 {
                    runInSeparateProcess true
                    jvmOptions = ["-Xmx2g", "-XX:+UseParallelGC"]
                }
            }
        }
    }
}

dependencyResolutionManagement {
    settings.ext.repos.google(repositories)
    settings.ext.repos.mavenCentral(repositories)
}

apply from: "gradle/version_catalogs.gradle"

include ':app', ':dynamicfeature1', ":shared"
include ":lib:hostconfig"
rootProject.name='My Application'
